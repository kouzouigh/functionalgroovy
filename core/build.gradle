
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: "maven"

defaultTasks 'build', 'uploadLocalArchive'

ext.artifactId = "$projectName-core"

repositories {
	ivy {
		name = 'DynamicRepo'
		ivyPattern "${repoGenBase}/[organisation]/[module]/ivy-[revision].xml"
		artifactPattern "${repoGenBase}/[organisation]/[module]/[type]s/[artifact]-[revision].[ext]"
	}
	mavenCentral()
	maven {
		url nexusUrlGroups
	}
}

configurations {
	corporateNexus
	localArchive
}

artifacts {
	localArchive jar
	corporateNexus jar
}

dependencies {
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.4'
	compile group: 'org.functionaljava', name: 'functionaljava', version: '3.1'
}

jar {
	baseName "$artifactId"
	manifest {
		attributes 'Created-By': founder, 'Signature-Version': version
	}
}

task display << {
	println "$organisation $groupId:$artifactId:$version"
}

uploadCorporateNexus {
	repositories {
		mavenDeployer {
			repository(url: nexusCorporateReleaseRepo) {
				authentication(userName: nexusCorporateUsername, password: nexusCorporatePassword)
			}
			pom.version = version
			pom.artifactId = artifactId
			pom.groupId = groupId
		}
	}
}

uploadLocalArchive {
	repositories {
		add project.repositories.DynamicRepo
	}
}
