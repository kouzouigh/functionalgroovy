
apply from: "properties.gradle"

allprojects {

	defaultTasks 'build'
}

subprojects {
	apply plugin: "signing"
	apply plugin: "groovy"
	apply plugin: "java"
	apply plugin: "maven"



	version = "0.3-SNAPSHOT"

	ext {
		sonatypeSnapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
		sonatypeReleaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"


//		name = "functionalgroovy-$name"
		projectName = "functionalgroovy"
		baseJarName = "$projectName-$name"


		groupId = "com.github.mperry"
		sourceCompatibility = "1.7"
		founder = "Mark Perry"
		organisation = "com.github.mperry"
//		#groovyVersion = 2.1.5
		groovyVersion = "2.1.6"
//		#groovyVersion = 2.1+
//		#groovyVersion = 2.2.0-rc-2
//		#groovyVersion = 2.2+

	}


	repositories {
		mavenCentral()
	}


	jar {
		baseName baseJarName
		manifest {
			attributes 'Created-By': project.founder, 'Signature-Version': project.version
		}
	}

	task groovydocJar(type: Jar, dependsOn: groovydoc) {
		classifier = 'javadoc'
		from 'build/docs/groovydoc'
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	artifacts {
		archives jar
		archives groovydocJar
		archives sourcesJar
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: sonatypeSnapshotUrl) {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}

				pom.project {
					name 'FunctionalGroovy'
					packaging 'jar'
					description 'FunctionalGroovy enhances FunctionalJava with Groovy idioms'
					url 'https://github.com/mperry/functionalgroovy'

					scm {
						url 'https://github.com/mperry/functionalgroovy.git'
						connection 'scm:git@github.com:mperry/functionalgroovy.git'
						developerConnection 'scm:git@github.com:mperry/functionalgroovy.git'
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'mperry'
							name 'Mark Perry'
						}
					}
				}
			}
		}
	}

}

task clean << {
	file("$projectDir/build").deleteDir()
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.6'
}
