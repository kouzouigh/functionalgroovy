
allprojects {
	defaultTasks 'build'
	
}

subprojects {
	apply plugin: "signing"
	apply plugin: "groovy"
	apply plugin: "java"
	apply plugin: "maven"

    ext {
        snap = "-SNAPSHOT"
    }
	version = "0.5$snap".toString()

	ext {
        consumeVersion = "0.5-SNAPSHOT"

        // sonatypeUsername and sonatypePassword stored in %USERPROFILE%/.gradle/gradle.properties,
        // otherwise override with -PsonatypeUsername=user -PsonatypePassword=pwd
        if (!project.hasProperty("sonatypeUsername")) {
            sonatypeUsername = ""
        }
        if (!project.hasProperty("sonatypePassword")) {
            sonatypePassword = ""
        }
//        println "Sonatype credentials: sonatypeUsername $sonatypeUsername sonatypePassword $sonatypePassword"

		sonatypeBaseUrl = "https://oss.sonatype.org"
		sonatypeSnapshotUrl = "$sonatypeBaseUrl/content/repositories/snapshots/"
        sonatypeRepositoryUrl = "$sonatypeBaseUrl/content/groups/public"
		sonatypeReleaseUrl = "$sonatypeBaseUrl/service/local/staging/deploy/maven2/"
        sonatypeUploadUrl = version.endsWith(snap) ? sonatypeSnapshotUrl : sonatypeReleaseUrl

		projectUrl = "https://github.com/mperry/functionalgroovy"
		projectName = "functionalgroovy"
		pomProjectName = "FunctionalGroovy"
		baseJarName = "$projectName-$name"


		groupName = "com.github.mperry"
		scmGitFile = "scm:git@github.com:mperry/functionalgroovy.git"
		projectDescription = "FunctionalGroovy enhances FunctionalJava for Groovy"
		sourceCompatibility = "1.7"
		founder = "Mark Perry"
		founderUsername = "mperry"
		organisation = groupName
        groovyVersion = "2.2.+"

		gradleGroovy = "org.codehaus.groovy:groovy-all:$groovyVersion"
//		gradleFj = "$groupName:functionaljava:4.1-SNAPSHOT"
//        gradleFj = "org.functionaljava:functionaljava:4.1-SNAPSHOT"
        gradleFj = "org.functionaljava:functionaljava:3.1"
		gradleJunit = "junit:junit:4.10"

	}

    archivesBaseName = baseJarName
    group = project.groupName

	repositories {

		mavenCentral()
        mavenLocal()
	}

	jar {
		manifest {
			attributes 'Created-By': project.founder, 'Signature-Version': project.version
		}
	}

	task groovydocJar(type: Jar, dependsOn: groovydoc) {
		classifier = 'javadoc'
		from 'build/docs/groovydoc'
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	artifacts {
		archives jar
		archives groovydocJar
		archives sourcesJar
	}

    signing {
        sign configurations.archives
    }

    uploadArchives.enabled = false
	uploadArchives {
		repositories {
			mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: sonatypeUploadUrl) {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}
				pom {
					groupId = project.groupName
					project {
						name pomProjectName
						packaging 'jar'
						description projectDescription
						url projectUrl

						scm {
							url "${projectUrl}.git"
							connection scmGitFile
							developerConnection scmGitFile
						}

						licenses {
							license {
								name 'The Apache Software License, Version 2.0'
								url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
								distribution 'repo'
							}
						}

						developers {
							developer {
								id project.founderUsername
								name project.founder
							}
						}
					}

				}

			}
		}
	}

}
