
allprojects {
	defaultTasks 'build'
	
}

subprojects {
	apply plugin: "signing"
	apply plugin: "groovy"
	apply plugin: "java"
	apply plugin: "maven"

	version = "0.3-SNAPSHOT"

	ext {
		sonatypeBaseUrl = "https://oss.sonatype.org"
		sonatypeSnapshotUrl = "$sonatypeBaseUrl/content/repositories/snapshots/"
		sonatypeReleaseUrl = "$sonatypeBaseUrl/service/local/staging/deploy/maven2/"
		projectUrl = "https://github.com/mperry/functionalgroovy"
		projectName = "functionalgroovy"
		pomProjectName = "FunctionalGroovy"
		baseJarName = "$projectName-$name"
		groupName = "com.github.mperry"
		scmGitFile = "scm:git@github.com:mperry/functionalgroovy.git"
		projectDescription = "FunctionalGroovy enhances FunctionalJava for Groovy"
		sourceCompatibility = "1.7"
		founder = "Mark Perry"
		founderUsername = "mperry"
		organisation = groupName
		groovyVersion = "2.2.1"

		gradleGroovy = "org.codehaus.groovy:groovy-all:$groovyVersion"
		gradleFj = "org.functionaljava:functionaljava:3.1"
		gradleJunit = "junit:junit:4.10"

	}

	group = project.groupName

	repositories {
		mavenCentral()
	}

	jar {
		baseName baseJarName
		manifest {
			attributes 'Created-By': project.founder, 'Signature-Version': project.version
		}
	}

	task groovydocJar(type: Jar, dependsOn: groovydoc) {
		classifier = 'javadoc'
		from 'build/docs/groovydoc'
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	artifacts {
		archives jar
		archives groovydocJar
		archives sourcesJar
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: sonatypeSnapshotUrl) {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}

				pom {
					groupId = project.groupName
					project {
						name pomProjectName
						packaging 'jar'
						description projectDescription
						url projectUrl

						scm {
							url "${projectUrl}.git"
							connection scmGitFile
							developerConnection scmGitFile
						}

						licenses {
							license {
								name 'The Apache Software License, Version 2.0'
								url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
								distribution 'repo'
							}
						}

						developers {
							developer {
								id project.founderUsername
								name project.founder
							}
						}
					}

				}

			}
		}
	}

}
